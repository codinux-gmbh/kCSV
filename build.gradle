buildscript {
    dependencies {
        classpath 'net.sf.proguard:proguard-gradle:6.2.2'
    }
}

plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id 'checkstyle'
    id 'pmd'
    id 'jacoco'
    id 'com.github.spotbugs' version '4.5.0'
    id 'me.champeau.jmh' version '0.7.0'
    id 'info.solidsoft.pitest' version '1.5.2'
    id 'org.jetbrains.kotlin.jvm' version '1.8.10'
}

group = "net.codinux.csv"
version = "1.0.0-SNAPSHOT"

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(8)
    }
    withJavadocJar()
    withSourcesJar()
}

repositories {
    mavenCentral()
}

sourceSets {
    example
}

dependencies {
    compileOnly("org.jetbrains.kotlinx:kotlinx-datetime:0.4.0")

    testImplementation(platform('org.junit:junit-bom:5.7.0'))
    testImplementation('org.junit.jupiter:junit-jupiter')

    exampleImplementation sourceSets.main.output

    pitest 'org.pitest:pitest-junit5-plugin:0.12'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

tasks.matching { task -> task.name.startsWith('spotbugs') }.forEach {
    it.reports {
        html.enabled = true
        xml.enabled = false
    }
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

pitest {
    testPlugin = 'junit5'
    targetClasses = ['blackbox.*', 'de.siegmar.*']
    timestampedReports = false
}

pmd {
    consoleOutput = true
    ruleSets = []
    ruleSetFiles = files("config/pmd/config.xml")
}

jacocoTestReport {
    reports {
        xml.enabled true
    }
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                counter = 'BRANCH'
                value = 'COVEREDRATIO'
                minimum = 1.0
            }
        }
    }
}

check.dependsOn jacocoTestCoverageVerification

jmh {
    warmupIterations = 2
    iterations = 5
    benchmarkMode = ['thrpt']
    fork = 2
    operationsPerInvocation = 1
}

task proguardCheck(type: proguard.gradle.ProGuardTask, dependsOn: jar) {
    injars project.jar.archivePath
    libraryjars files(String.format("%s/platforms/android-%s/android.jar",
            System.getenv("ANDROID_HOME"), System.getenv("API_LEVEL")))
    libraryjars files(configurations.implementation)
    // Don't really try to do anything, we just want this tool for it's bytecode lint
    dontshrink
    dontobfuscate
    dontoptimize
    // Don't fail if LambdaMetafactory is missing - it would be handled by Retrolambda / Jack
    dontwarn 'java.lang.invoke.LambdaMetafactory'
}

tasks.named('jar') {
    manifest {
        attributes('Automatic-Module-Name': 'net.codinux.csv.kcsv')
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'kcsv'
            from components.kotlin

            pom {
                name = 'kCSV'
                description = 'Ultra fast and simple RFC 4180 compliant CSV library.'
                url = 'https://github.com/osiegmar/FastCSV'
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://opensource.org/licenses/MIT'
                    }
                }
                scm {
                    url = 'https://github.com/osiegmar/FastCSV'
                    connection = 'scm:git:https://github.com/osiegmar/FastCSV.git'
                }
                developers {
                    developer {
                        id = 'osiegmar'
                        name = 'Oliver Siegmar'
                        email = 'oliver@siegmar.de'
                    }
                }
            }
        }
    }
    repositories {
        maven {
            name = 'ossrh'
            credentials(PasswordCredentials)
            def releasesRepoUrl = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
            url version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}
